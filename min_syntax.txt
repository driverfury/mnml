VAR = 'var'
FUNC = 'func'

VOID = 'void'
CHAR = 'char'
INT = 'int'
STRUCT = 'struct'

RETURN = 'return'
IF = 'if'
ELSE = 'else'
WHILE = 'while'

CAST = 'cast'

SPMEMB = '->'

LESSEQ = '<='
GRTREQ = '>='
EQ = '=='
NEQ = '!='

IDENT = [_A-Za-z][_A-Za-z0-9]*
INTLIT = [0-9]*
       | '\'' . '\''
STRLIT = '"' .* '"'

<unit> = <glob_decl>*

<glob_decl> = VAR IDENT ':' <typespec> ';'
            | FUNC IDENT '(' <params>? ')' ':' <typespec> '{' (<decl> ',')* <stmt>* '}'
            | STRUCT IDENT <struct_def>
            | TYPEDEF IDENT ':' <typespec> ';'

<params> = <param> (','<param>))*

<stmt_block> = '{' <stmt>* '}'

<stmt> = IF '(' <expr> ')' <stmt> (ELSE <stmt>)?
       | WHILE '(' <expr> ')' <stmt>
       | RETURN <expr>? ';'
       | '{' <stmt>* '}'
       | IDENT '(' ')' ';'
       | <lvalue> '=' <expr> ';'

<lvalue> = IDENT
         | IDENT '[' <expr> ']'
         | IDENT '.' IDENT
         | IDENT SPMEMB IDENT
         | '*' <lvalue>
         | '(' <lvalue> ')'

<decl> = VAR IDENT ':' <typespec>

<typespec> = VOID
           | CHAR
           | INT
           | <typeref>
           | '[' typespec ']'
           | <typespec> '[' <expr> ']'

<struct_def> = '{' (IDENT ':' <typespec> ';')+ '}'

<args> = <expr> (',' <expr>)*

<expr_base> = INTLIT
            | STRLIT
            | IDENT
            | '(' <expr> ')'

<expr_first> = <expr_base> '(' <args>? ')'
             | <expr_base> '[' <expr> ']'
             | <expr_base> '.' IDENT
             | <expr_base> SPMEMB IDENT
             | <expr_base>

<expr_unary> = '+' <expr_unary>
             | '-' <expr_unary>
             | '*' <expr_unary>
             | '&' <expr_unary>
             | CAST '<' <typespec> '>' <expr_first>
             | <expr_first>

<expr_mul> = <expr_mul> '*' <expr_unary>
           | <expr_mul> '/' <expr_unary>
           | <expr_mul> '%' <expr_unary>
           | <expr_unary>

<expr_add> = <expr_add> '+' <expr_mul>
           | <expr_add> '-' <expr_mul>
           | <expr_mul>

<expr_rel> = <expr_rel> '<' <expr_add>
           | <expr_rel> LESSEQ <expr_add>
           | <expr_rel> '>' <expr_add>
           | <expr_rel> GRTREQ <expr_add>
           | <expr_add>

<expr_eq> = <expr_eq> EQ <expr_rel>
          | <expr_eq> NEQ <expr_rel>
          | <expr_rel>

<expr_logic> = <expr_logic> AND <expr_eq>
             | <expr_logic> OR <expr_eq>
             | <expr_eq>

<expr> = <expr_logic>
